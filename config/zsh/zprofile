# vim: ts=2: sts=2: sw=2:

#
# Executes commands at login pre-zshrc.
# This file is similar to zlogin, but it is sourced before zshrc. It was added for KornShell fans. See the description of zlogin below for what it may contain.
# zprofile and zlogin are not meant to be used together but can be done so.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ -z "$BROWSER" && "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

function check_command () {
    command -v $1 >/dev/null
}

if [[ -z "$EDITOR" ]]; then
    if ( check_command nvim ); then
        export EDITOR='nvim'
    elif ( check_command vim ); then
        export EDITOR='vim'
    fi
fi

if [[ -z "$VISUAL" ]]; then
  export VISUAL="$EDITOR"
fi
if [[ -z "$PAGER" ]]; then
  export PAGER='less'
fi

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Timezone
# Use system default timezone
#


#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Node
NPM_PACKAGES="${HOME}/.npm-packages"
export NODE_PATH="$NPM_PACKAGES/lib/node_modules:$NODE_PATH"

# Set the list of directories that Zsh searches for programs.
# (N) to remove the path when the path is not found.
path=(
  $HOME/{,s}bin                         # Customized binary/script managed by git
  $HOME/docker-bin(N)

  $HOME/.codeium/windsurf/bin           # Windsurf

  # Programing Language and packages
  $HOME/.cargo/bin(N)                   # Rust: Local install path
  $HOME/Library/Python/*/bin(N)         # Python: Local pip path for OSX
  $NPM_PACKAGES/bin(N)                  # Node: Local npm packages path

  /opt/homebrew/opt/ruby/bin(N)         # Ruby: Homebrew Ruby
  /opt/homebrew/lib/ruby/gems/*/bin(N)  # Ruby: Homebrew Gem path
  $HOME/.gem/ruby/*/bin(N)              # Ruby: Local gem path

  # Homebrew binary
  /opt/homebrew/{,s}bin(N)              # Default homebrew for Apple Silicon

  # Latex
  /Library/TeX/texbin(N)                # Mactex
  /usr/texbin(N)                        # Linux latex

  # Work related
  $HOME/working/cros_script/bin(N)
  $HOME/depot_tools(N)
  $HOME/crosfleet(N)

  $path
)

# Ensure path arrays do not contain duplicates.
typeset -gU path


#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X to enable it.
if [[ -z "$LESS" ]]; then
  export LESS='-g -i -M -R -S -w -X -z-4'
fi

# set display charset of less
export LESSCHARSET="UTF-8"

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if [[ -z "$LESSOPEN" ]] && (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

#
# ls colors
#
# Tool: https://geoff.greer.fm/lscolors/

# BSD (mac)
export LSCOLORS="Gxfxcxdxbxegedabagacad"
# Linux
export LS_COLORS="di=1;36:ln=35:so=32:pi=33:ex=31:bd=34;46:cd=34;43:su=30;41:sg=30;46:tw=30;42:ow=30;43"
# zsh (use Linux format)
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
